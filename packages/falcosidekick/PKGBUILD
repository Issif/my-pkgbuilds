# Maintainer: brokenpip3 <brokenpip3[at]gmail[dot]com>
# https://github.com/brokenpip3/my-pkgbuilds
# Contributors:
# - Thomas Labarussias <issif+aur@gadz.org>

pkgname=falcosidekick
pkgver=2.28.0
pkgrel=1
pkgdesc="Connect Falco to your ecosystem"
arch=('x86_64')
url="https://github.com/falcosecurity/falcosidekick"
license=('MIT')
makedepends=('go' 'git' 'make')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('bb9c3fc484514526c40174423a0ca26f38c3a26cd3c0b76b442598693cfc00ff')

build() {
  cd "$pkgname-$pkgver"
  make falcosidekick
}

check() {
  cd "$pkgname-$pkgver"
  make test
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm755 falcosidekick "${pkgdir}/usr/bin/falcosidekick"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
