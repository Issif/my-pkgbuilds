# Maintainer: brokenpip3 <brokenpip3[at]gmail[dot]com>
# https://github.com/brokenpip3/my-pkgbuilds
# Contributor: Kris NÃ³va <kris@nivenly.com> R.I.P.

pkgbase=falco-probe-ebpf
pkgname=falco-probe-ebpf
pkgdesc="Cloud native runtime security - eBPF probe"
pkgver=0.38.1
pkgrel=1
arch=(x86_64)
license=(Apache)
depends=(falco)
makedepends=(cmake git c-ares jq grpc yaml-cpp clang linux-headers llvm)
url="https://github.com/falcosecurity/falco"
source_x86_64=(
    "falco-${pkgver}.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz"
    "falco-ebpf.service"
)
sha256sums_x86_64=('0e5cb8f527e3c55179a12ab312ee8955532d6d9ba88f100aa60afc50244e85da'
                   '79705a952d3cc7f646bcdce6e79419996271959668b9e59ca734255114c70de0')
install="falco-ebpf.install"

prepare() {
    cd "${srcdir}/falco-${pkgver}"
    [[ -d build ]] || mkdir build
}

build() {
    cd "${srcdir}/falco-${pkgver}/build"
    cmake .. \
        -DCMAKE_BUILD_TYPE=None \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DUSE_BUNDLED_DEPS=false \
        -DBUILD_BPF=ON
    make bpf
}

package() {
    install -Dm644 "${srcdir}/falco-${pkgver}/build/driver/bpf/probe.o" "${pkgdir}/usr/share/falco/falco-bpf.o"
    install -Dm644 "${srcdir}/falco-ebpf.service" "${pkgdir}/usr/lib/systemd/system/falco-ebpf.service"
}
